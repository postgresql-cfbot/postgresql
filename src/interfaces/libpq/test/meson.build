# Copyright (c) 2022-2023, PostgreSQL Global Development Group

libpq_uri_regress_sources = files(
  'libpq_uri_regress.c',
)

if host_system == 'windows'
  libpq_uri_regress_sources += rc_bin_gen.process(win32ver_rc, extra_args: [
    '--NAME', 'libpq_uri_regress',
    '--FILEDESC', 'libpq test program',])
endif

executable('libpq_uri_regress',
  libpq_uri_regress_sources,
  dependencies: [frontend_code, libpq],
  kwargs: default_bin_args + {
    'install': false,
  }
)


libpq_testclient_sources = files(
  'libpq_testclient.c',
)

if host_system == 'windows'
  libpq_testclient_sources += rc_bin_gen.process(win32ver_rc, extra_args: [
    '--NAME', 'libpq_testclient',
    '--FILEDESC', 'libpq test program',])
endif

executable('libpq_testclient',
  libpq_testclient_sources,
  dependencies: [frontend_code, libpq],
  kwargs: default_bin_args + {
    'install': false,
  }
)


libpq_test_encrypt_sources = files(
  '../fe-encrypt-openssl.c',
)

if host_system == 'windows'
  libpq_test_encrypt_sources += rc_bin_gen.process(win32ver_rc, extra_args: [
    '--NAME', 'libpq_test_encrypt',
    '--FILEDESC', 'libpq test program',])
endif

if ssl.found()
  executable('libpq_test_encrypt',
    libpq_test_encrypt_sources,
    include_directories: include_directories('../../../port'),
    dependencies: [frontend_code, libpq, ssl],
    c_args: ['-DTEST_ENCRYPT'],
    kwargs: default_bin_args + {
      'install': false,
    }
  )
endif
